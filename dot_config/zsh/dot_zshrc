# Zsh settings for interactive shells.

# Set terminal colors before running tmux.
if [[ -n $WAYLAND_DISPLAY ]] && [[ -z $TMUX ]] && [[ -z $NO_TMUX ]]; then
    if [[ -n "$PS1" ]]; then
        GRUVBOX=$HOME/.config/gruvbox.sh
        if [[ -f $GRUVBOX ]]; then
            source $GRUVBOX
        fi
    fi
    exec env tmux new-session -A -s 0
fi

# Zsh options.
setopt \
  append_history \
  share_history \
  extended_history \
  histignorespace \
  hist_find_no_dups \
  hist_ignore_all_dups \
  hist_reduce_blanks \
  no_case_glob \
  extended_glob \
  noglobdots \
  correct \
  completeinword \
  longlistjobs \
  notify \
  hash_list_all \
  nohup \
  auto_pushd \
  pushd_ignore_dups \
  prompt_subst \
  nobeep \
  noshwordsplit \
  noclobber \
  unset \

# Zsh history variables.
HISTFILE=$ZDOTDIR/.zhistory
HISTSIZE=100000
SAVEHIST=$HISTSIZE

# Prompt
function hline {
  print ${(pl:$COLUMNS::\u2500:)}
}
function last_exit_code() {
  local EXIT_CODE=$?
  if [[ $EXIT_CODE -ne 0 ]]; then
    echo "[$EXIT_CODE] "
  fi
}
function venv_status {
  if [[ -n "$VIRTUAL_ENV" ]]; then
    echo "(venv) "
  fi
}
function width {
  echo $(( COLUMNS - 24 ))
}
function ssh_hostname {
  if [[ -n "$SSH_TTY" ]]; then
    echo "%n@%M:"
  fi
}
PROMPT='%F{237}$(hline)'$'\n''%K{237}%F{4}$%f%k '
PROMPT2='%K{237}%F{4}%_>%f%k '
RPROMPT='%F{5}$(last_exit_code)$(venv_status)%$(width)<â€¦<$(ssh_hostname)%~%<<%f'

# Plugins.
autoload -Uz \
  compinit \
  up-line-or-beginning-search down-line-or-beginning-search
compinit
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ':completion:*' completer _expand_alias _complete _ignored

zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search

# Key bindings.
bindkey -e
bindkey '^P' up-line-or-beginning-search
bindkey '^N' down-line-or-beginning-search

# Create aliases for all uutils commands. Don't try to alias the '[' command.
for full_cmd in /usr/bin/uu-*(N); do
  uu_cmd=${full_cmd##*/}
  cmd=${uu_cmd#uu-}
  if [[ $cmd != '[' ]]; then
    alias "$cmd"="$uu_cmd"
  fi
done

# Aliases.
alias ls='ls'
alias i='ls'
alias ll='ls -l'
alias la='ls -a'
alias lla='ls -la'
alias cp='cp -i'
alias diff='diff --color=auto'
alias du='dust'
alias grep='grep --color=auto'
alias ip='ip -color=auto'
alias less='less -w'
alias mv='mv -i'
alias tar='tar --keep-old-files'
alias veracrypt='veracrypt --text'
alias vim='nvim'
alias cal='cal --monday --year --week'
alias fast='fast --upload --single-line'

# When not in a pipe, run sudo -v to refresh timeout.
sudo() {
    if [[ -t 0 ]]; then
        command sudo -v
    fi
    command sudo "$@"
}

# Backups.
restic_() {
    RESTIC_REPOSITORY=gs:cwo-restic:/ \
    RESTIC_PASSWORD_FILE="$HOME/.keys/restic-password" \
    GOOGLE_PROJECT_ID=restic-377922 \
    GOOGLE_APPLICATION_CREDENTIALS="$HOME/.keys/restic-service-account-key.json" \
    restic "$@"
}

backup() {
    local args=(--exclude-file="$HOME/.config/restic/excludes.txt" "$HOME/files" "$@")
    local usb_repo=/mnt/usb/restic-repo
    if [[ -d $usb_repo ]]; then
        restic_ --repo="$usb_repo" backup "${args[@]}"
    fi
    restic_ backup "${args[@]}"
}

# Execute private zshrc.
ZSH_PRIVATE=~/.config/zsh/.zshrc_private
if [[ -f $ZSH_PRIVATE ]]; then
    source $ZSH_PRIVATE
fi

# direnv, per-directory configuration during cd. https://direnv.net/
if command -v direnv &>/dev/null; then
    eval "$(direnv hook zsh)"
fi

# vim: set ts=2 sts=2 sw=2 expandtab:
